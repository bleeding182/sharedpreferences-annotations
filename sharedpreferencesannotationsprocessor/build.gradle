/*
 * The MIT License (MIT)
 *
 * Copyright (c) 2015 David Medenjak
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
buildscript {
    repositories {
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':sharedpreferencesannotations')
    compile 'com.squareup:javawriter:2.5.1'
    compile 'com.google.android:android:4.1.1.4'
}

archivesBaseName = "processor"

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

def ossrhUsername = System.properties['ossrhUsername']
def ossrhPassword = System.properties['ossrhPassword']

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'SharedPreferences Annotation Processor'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Annotation Processor to generate wrapped SharedPreferences with custom accessor methods.'
                url 'https://github.com/bleeding182/sharedpreferences-annotations'

                scm {
                    connection 'scm:git:ssh://git@github.com/bleeding182/sharedpreferences-annotations.git'
                    developerConnection 'scm:git:ssh://git@github.com/bleeding182/sharedpreferences-annotations.git'
                    url 'https://github.com/bleeding182/sharedpreferences-annotations'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'bleeding182'
                        name 'David Medenjak'
                        email 'davidmedenjak@gmail.com'
                        url 'https://github.com/bleeding182 '
                        roles {
                            role 'owner'
                            role 'developer'
                        }
                        timezone '+1'
                    }
                }
            }
        }
    }
}